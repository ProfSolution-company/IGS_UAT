
&НаСервере
Функция ПолучитьРасшифровкуПоДокументам(СтруктураЗначений)
	ОтчетОбъект = РеквизитФормыВЗначение("Отчет");
	Возврат ОтчетОбъект.ПолучитьРасшифровкуПоДокументам(СтруктураЗначений);
КонецФункции

&НаКлиенте
Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка, ДополнительныеПараметры)
	
	_ДанныеРасшифровки = ПолучитьИзВременногоХранилища(ДанныеРасшифровки);
	ИмяПоля = _ДанныеРасшифровки.Элементы[Расшифровка].ПолучитьПоля()[0].Поле;
	Если Лев(ИмяПоля, 1) <> "д" Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураЗначений = Новый Структура;
	ТекИдентификатор = Расшифровка;
	Пока Истина Цикл
		Попытка
			Для каждого _Поле Из _ДанныеРасшифровки.Элементы[ТекИдентификатор].ПолучитьПоля() Цикл
				Ключ = _Поле.Поле;
				Пока Найти(Ключ, ".") > 0 Цикл
					Ключ = Сред(Ключ, Найти(Ключ, ".") + 1);
				КонецЦикла;
				Значение = _Поле.Значение;
				Если НЕ СтруктураЗначений.Свойство(Ключ) Тогда
					СтруктураЗначений.Вставить(Ключ, Значение);
				КонецЕсли;
			КонецЦикла;
		Исключение
		КонецПопытки;
		Родители = _ДанныеРасшифровки.Элементы[ТекИдентификатор].ПолучитьРодителей();
		Если Родители.Количество() = 0 Тогда
			Прервать;
		КонецЕсли;
		ТекИдентификатор = _ДанныеРасшифровки.Элементы[ТекИдентификатор].ПолучитьРодителей()[0].Идентификатор;
	КонецЦикла;
	
	Если НЕ СтруктураЗначений.Свойство("ТранспортноеСредство") Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	НачалоПериодаОтчета = Отчет.КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("НачалоПериодаОтчета")).Значение;
	СмещениеДаты = ?(ИмяПоля = "д", 0, Число(Сред(ИмяПоля, 2)));
	СтруктураЗначений.Вставить("ДатаДетализации", НачалоПериодаОтчета + СмещениеДаты * 86400);
	
	ТабРасшифровки = ПолучитьРасшифровкуПоДокументам(СтруктураЗначений);
	Если ТабРасшифровки <> Неопределено Тогда
		ТабРасшифровки.ТолькоПросмотр = Истина;
		ТабРасшифровки.Показать("Расшифровка по документам");
	КонецЕсли;              
	
КонецПроцедуры
