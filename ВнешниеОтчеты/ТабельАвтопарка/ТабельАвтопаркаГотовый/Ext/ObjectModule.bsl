Перем ОписаниеТипаСтрока;
Перем ОписаниеТипаЧисло;

Функция СведенияОВнешнейОбработке() Экспорт 

	РегистрационныеДанные = Новый Структура();
	РегистрационныеДанные.Вставить("Наименование", "Отчет табель автопарка");
	РегистрационныеДанные.Вставить("БезопасныйРежим", Ложь);
	РегистрационныеДанные.Вставить("Версия", "v 1.0");
	РегистрационныеДанные.Вставить("Информация", "Отчет табель автопарка");
	РегистрационныеДанные.Вставить("Вид", "ДополнительныйОтчет");
	
	тз = Новый ТаблицаЗначений;
	тз.Колонки.Добавить("Идентификатор");
	тз.Колонки.Добавить("Использование");
	тз.Колонки.Добавить("Представление");
	
	НоваяСтрока = тз.Добавить();
	НоваяСтрока.Идентификатор = "ОткрытьОтчет";
	НоваяСтрока.Использование = "ОткрытиеФормы";
	НоваяСтрока.Представление = "Отчет по путевым листам";
	
	РегистрационныеДанные.Вставить("Команды", тз);
	
	Возврат РегистрационныеДанные;

	
КонецФункции // ()


Функция ПолучитьТаблицуПростоев()

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачПериода", НачалоДня(НачПериода));
	Запрос.УстановитьПараметр("КонПериода", КонецДня(КонПериода));
	Запрос.Текст =  "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                |	прНевыходТехникиОбороты.Период КАК Дата,
	                |	прНевыходТехникиОбороты.Организация КАК Организация,
	                |	прНевыходТехникиОбороты.Колонна КАК Колонна,
	                |	прНевыходТехникиОбороты.ТранспортноеСредство КАК ТранспортноеСредство,
	                |	прНевыходТехникиОбороты.ПричинаНевыхода КАК ПричинаНевыхода,
	                |	прНевыходТехникиОбороты.ПричинаНевыхода.Код КАК Код,
	                |	прНевыходТехникиОбороты.КоличествоОборот КАК Количество
	                |ПОМЕСТИТЬ ВТ_НевыходТехники
	                |ИЗ
	                |	РегистрНакопления.прНевыходТехники.Обороты(&НачПериода, &КонПериода, День, ) КАК прНевыходТехникиОбороты
	                |;
	                |
	                |////////////////////////////////////////////////////////////////////////////////
	                |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                |	ВТ_НевыходТехники.Дата КАК Дата,
	                |	ВТ_НевыходТехники.Организация КАК Организация,
	                |	ВТ_НевыходТехники.Колонна КАК Колонна,
	                |	ВТ_НевыходТехники.ТранспортноеСредство КАК ТранспортноеСредство,
	                |	ВТ_НевыходТехники.ПричинаНевыхода КАК ПричинаНевыхода,
	                |	ВТ_НевыходТехники.Код КАК Код,
	                |	ВТ_НевыходТехники.Количество КАК Количество,
	                |	ЕСТЬNULL(уатТС.Модель, ЗНАЧЕНИЕ(Справочник.уатМоделиТС.ПустаяСсылка)) КАК Модель
	                |ИЗ
	                |	ВТ_НевыходТехники КАК ВТ_НевыходТехники
	                |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.уатТС КАК уатТС
	                |		ПО ВТ_НевыходТехники.ТранспортноеСредство = уатТС.Ссылка"; 			   
				   
	ТаблицаПростоев = Запрос.Выполнить().Выгрузить();
	
	// Дополнение таблицы простоев данными по ремонтным листам
	МассивСостоянийРемонта = Новый Массив;
	МассивСостоянийРемонта.Добавить(Справочники.уатСостояниеТС.НаКапитальномРемонте);
	МассивСостоянийРемонта.Добавить(Справочники.уатСостояниеТС.НаСреднемРемонте);
	МассивСостоянийРемонта.Добавить(Справочники.уатСостояниеТС.НаТекущемРемонте);
	Запрос.УстановитьПараметр("СостоянияРемонта", МассивСостоянийРемонта);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	уатТС.Ссылка КАК Ссылка,
	               |	уатТС.Модель КАК Модель
	               |ПОМЕСТИТЬ ВТ_ДанныеТС
	               |ИЗ
	               |	Справочник.уатТС КАК уатТС
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_ДанныеТС.Модель КАК Модель,
	               |	ВЫБОР
	               |		КОГДА уатСостояниеТССрезПоследних.ДатаОкончания > &КонПериода
	               |				ИЛИ уатСостояниеТССрезПоследних.ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1)
	               |			ТОГДА &КонПериода
	               |		ИНАЧЕ уатСостояниеТССрезПоследних.ДатаОкончания
	               |	КОНЕЦ КАК ОкончаниеРаботы,
	               |	уатСостояниеТССрезПоследних.Период КАК НачалоРаботы,
	               |	уатСостояниеТССрезПоследних.ТС КАК ТранспортноеСредство
	               |ИЗ
	               |	ВТ_ДанныеТС КАК ВТ_ДанныеТС
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.уатСостояниеТС.СрезПоследних КАК уатСостояниеТССрезПоследних
	               |		ПО ВТ_ДанныеТС.Ссылка = уатСостояниеТССрезПоследних.ТС
	               |ГДЕ
	               |	уатСостояниеТССрезПоследних.Состояние В(&СостоянияРемонта)
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ВТ_ДанныеТС.Модель,
	               |	ВЫБОР
	               |		КОГДА уатСостояниеТС.ДатаОкончания > &КонПериода
	               |				ИЛИ уатСостояниеТС.ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1)
	               |			ТОГДА &КонПериода
	               |		ИНАЧЕ уатСостояниеТС.ДатаОкончания
	               |	КОНЕЦ,
	               |	уатСостояниеТС.Период,
	               |	уатСостояниеТС.ТС
	               |ИЗ
	               |	РегистрСведений.уатСостояниеТС КАК уатСостояниеТС
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ДанныеТС КАК ВТ_ДанныеТС
	               |		ПО уатСостояниеТС.ТС = ВТ_ДанныеТС.Ссылка
	               |ГДЕ
	               |	уатСостояниеТС.Состояние В(&СостоянияРемонта)
	               |	И уатСостояниеТС.Период МЕЖДУ &НачПериода И &КонПериода
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ТранспортноеСредство,
	               |	НачалоРаботы
	               |АВТОУПОРЯДОЧИВАНИЕ";    
	
	СтруктураОтбора = Новый Структура("Дата, ТранспортноеСредство");
	Выборка 		= Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.Модель.ТипТС.ВидТС = Перечисления.уатВидыТС.Прицеп ИЛИ
			 Выборка.Модель.ТипТС.ВидТС = Перечисления.уатВидыТС.ПрицепСамосвал ИЛИ
			 Выборка.Модель.ТипТС.ВидТС = Перечисления.уатВидыТС.ПрицепЦистерна ИЛИ
			 Выборка.Модель.ТипТС.ВидТС = Перечисления.уатВидыТС.Полуприцеп Тогда
			 
			Продолжить;
			
		КонецЕсли;
		
		ТекДата = Выборка.НачалоРаботы;
		Пока ТекДата < Выборка.ОкончаниеРаботы Цикл
			Если ТекДата >= НачалоДня(НачПериода) И
				 ТекДата <= КонецДня(КонПериода) Тогда
				
				НетРегистрацииРемонта = Истина;
				СтруктураОтбора.Дата = НачалоДня(ТекДата);
				СтруктураОтбора.ТранспортноеСредство = Выборка.ТранспортноеСредство;
				СтрокиТаблицаПростоев = ТаблицаПростоев.НайтиСтроки(СтруктураОтбора);
				Для каждого СтрокаТаблицаПростоев Из СтрокиТаблицаПростоев Цикл
					Если СтрокаТаблицаПростоев.ПричинаНевыхода = Справочники.прПричиныНевыходаТС.Ремонт Тогда
						НетРегистрацииРемонта = Ложь;
						Прервать;
					КонецЕсли;
				КонецЦикла;
				
				Если НетРегистрацииРемонта Тогда
					ДанныеМестонахожденияТС = РегистрыСведений.уатМестонахождениеТС.ПолучитьПоследнее(ТекДата, Новый Структура("ТС", Выборка.ТранспортноеСредство));
					СтрокаТаблицаПростоев = ТаблицаПростоев.Добавить();
					СтрокаТаблицаПростоев.Дата = НачалоДня(ТекДата);
					СтрокаТаблицаПростоев.Организация = ДанныеМестонахожденияТС.Организация;
					СтрокаТаблицаПростоев.Колонна = ДанныеМестонахожденияТС.Колонна;
					//СтрокаТаблицаПростоев.КолоннаФакт = ДанныеМестонахожденияТС.КолоннаФакт;
					СтрокаТаблицаПростоев.ТранспортноеСредство = Выборка.ТранспортноеСредство;
					СтрокаТаблицаПростоев.Модель = Выборка.Модель;
					СтрокаТаблицаПростоев.ПричинаНевыхода = Справочники.прПричиныНевыходаТС.Ремонт;
					СтрокаТаблицаПростоев.Код = Справочники.прПричиныНевыходаТС.Ремонт.Код;
					СтрокаТаблицаПростоев.Количество = 1;
				КонецЕсли;
			КонецЕсли;
			
			ТекДата = ТекДата + 86400;
		КонецЦикла;
	КонецЦикла;
	
	Возврат ТаблицаПростоев;	

КонецФункции // ()

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	ОписаниеТипаСтрока = Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(10));
	ОписаниеТипаЧисло = Новый ОписаниеТипов("Число");
	
	ЦветФонаЗавизированные = Новый Цвет(200, 255, 200);
	ЦветФонаРассчитанные = Новый Цвет(255, 255, 200);
	ЦветФонаНеПодписанные = Новый Цвет(255, 255, 0);
	ЦветФонаПересечения = Новый Цвет(255, 0, 0);
	
	Если НЕ ЗначениеЗаполнено(НачПериода) ИЛИ
		НЕ ЗначениеЗаполнено(КонПериода) ИЛИ
		НачПериода > КонПериода ИЛИ
		(КонПериода - НачПериода)/86400 > 100 Тогда
		#Если Клиент Тогда
			ПоказатьПредупреждение(, "Период отчета указан неверно", 60);
		#КонецЕсли
		Возврат;
	КонецЕсли; 
	
	ТаблицаПростоев = ПолучитьТаблицуПростоев();	
   	ТаблицаВидовПростоев = ТаблицаПростоев.Скопировать(, "ПричинаНевыхода, Код");
	ТаблицаВидовПростоев.Свернуть("ПричинаНевыхода, Код");
	ТаблицаВидовПростоев.Сортировать("Код");
	
	// Создание полей с датами отчета
	Если НачалоМесяца(НачПериода) = НачалоМесяца(КонПериода) Тогда
		ФорматнаяСтрокаДаты = "ДФ=dd";
	Иначе
		ФорматнаяСтрокаДаты = "ДФ=dd.MM";
	КонецЕсли;

	ТекДата = НачПериода;
	ИндексКолонки = 0;
	Пока ТекДата <= КонПериода Цикл
		ИмяКолонки = "д" + Формат(ИндексКолонки, "ЧГ=");
		
		// Представление по дате
		НовоеПоле = СхемаКомпоновкиДанных.НаборыДанных.НаборДанных1.Поля.Добавить(Тип("ПолеНабораДанныхСхемыКомпоновкиДанных"));
		НовоеПоле.ПутьКДанным = ИмяКолонки;
		НовоеПоле.Поле = ИмяКолонки;
		НовоеПоле.Заголовок = Формат(ТекДата, ФорматнаяСтрокаДаты);
		НовоеПоле.ТипЗначения = ОписаниеТипаСтрока;
		
		ПолеИтога = СхемаКомпоновкиДанных.ПоляИтога.Добавить();
		ПолеИтога.ПутьКДанным = ИмяКолонки;
		ПолеИтога.Выражение = "Максимум(" + ИмяКолонки + ")";
		ПолеИтога.Группировки.Добавить("ТранспортноеСредство");
		
		// Статус по дате
		ИмяКолонки = "с" + Формат(ИндексКолонки, "ЧГ=");
		НовоеПоле = СхемаКомпоновкиДанных.НаборыДанных.НаборДанных1.Поля.Добавить(Тип("ПолеНабораДанныхСхемыКомпоновкиДанных"));
		НовоеПоле.ПутьКДанным = ИмяКолонки;
		НовоеПоле.Поле = ИмяКолонки;
		НовоеПоле.Заголовок = ИмяКолонки;
		НовоеПоле.ТипЗначения = ОписаниеТипаЧисло;
		НовоеПоле.Роль.Обязательное = Истина;
		
		ПолеИтога = СхемаКомпоновкиДанных.ПоляИтога.Добавить();
		ПолеИтога.ПутьКДанным = ИмяКолонки;
		ПолеИтога.Выражение = "Минимум(" + ИмяКолонки + ")";
		ПолеИтога.Группировки.Добавить("ТранспортноеСредство");
		
		ТекДата = ТекДата + 86400;
		ИндексКолонки = ИндексКолонки + 1;
	КонецЦикла;

	// Создание полей по видам простоя
	ИндексКолонки = 0;
	Для каждого СтрокаТаблицаВидовПростоев Из ТаблицаВидовПростоев Цикл
		ИмяКолонки = "п" + Формат(ИндексКолонки, "ЧГ=");
		
		НовоеПоле = СхемаКомпоновкиДанных.НаборыДанных.НаборДанных1.Поля.Добавить(Тип("ПолеНабораДанныхСхемыКомпоновкиДанных"));
		НовоеПоле.ПутьКДанным = ИмяКолонки;
		НовоеПоле.Поле = ИмяКолонки;
		НовоеПоле.Заголовок = СокрЛП(СтрокаТаблицаВидовПростоев.ПричинаНевыхода.НаименованиеСокращенное);
		НовоеПоле.ТипЗначения = ОписаниеТипаЧисло;
		
		ПолеИтога = СхемаКомпоновкиДанных.ПоляИтога.Добавить();
		ПолеИтога.ПутьКДанным = ИмяКолонки;
		ПолеИтога.Выражение = "Сумма(" + ИмяКолонки + ")";
		
		ИндексКолонки = ИндексКолонки + 1;
	КонецЦикла;	 
	
	// Инициализация компоновщика
	КомпоновщикНастроекОтчета = Новый КомпоновщикНастроекКомпоновкиДанных;
	КомпоновщикНастроекОтчета.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновкиДанных));
	КомпоновщикНастроекОтчета.ЗагрузитьНастройки(КомпоновщикНастроек.Настройки);
	КомпоновщикНастроекОтчета.Настройки.ПараметрыВывода.УстановитьЗначениеПараметра("Заголовок", "Табель автопарка за " + ПредставлениеПериода(НачалоДня(НачПериода), КонецДня(КонПериода)));	
	
	// Создание полей компоновщика по дням
	ГруппаДни = Неопределено;
	Для каждого ПолеКомпоновкиДанных Из КомпоновщикНастроекОтчета.Настройки.Выбор.Элементы Цикл
		Если ТипЗнч(ПолеКомпоновкиДанных) = Тип("ГруппаВыбранныхПолейКомпоновкиДанных") И
			ПолеКомпоновкиДанных.Заголовок = "Дни" Тогда
			ГруппаДни = ПолеКомпоновкиДанных;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	ГруппаПростои = Неопределено;
	Для каждого ПолеКомпоновкиДанных Из КомпоновщикНастроекОтчета.Настройки.Выбор.Элементы Цикл
		Если ТипЗнч(ПолеКомпоновкиДанных) = Тип("ГруппаВыбранныхПолейКомпоновкиДанных") И
			ПолеКомпоновкиДанных.Заголовок = "Простои" Тогда
			ГруппаПростои = ПолеКомпоновкиДанных;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Если ГруппаПростои = Неопределено Тогда
		ГруппаПростои = КомпоновщикНастроекОтчета.Настройки.Выбор.Элементы.Добавить(Тип("ГруппаВыбранныхПолейКомпоновкиДанных"));
		ГруппаПростои.Заголовок = "Простои";
	КонецЕсли;
	
	ТекДата = НачПериода;
	ИндексКолонки = 0;
	Пока ТекДата <= КонПериода Цикл
		ИмяКолонки = "д" + Формат(ИндексКолонки, "ЧГ=");
		
		Если ГруппаДни = Неопределено Тогда
			ПолеКомпоновкиДанных = КомпоновщикНастроекОтчета.Настройки.Выбор.Элементы.Вставить(ИндексКолонки, Тип("ВыбранноеПолеКомпоновкиДанных"));
		Иначе
			ПолеКомпоновкиДанных = ГруппаДни.Элементы.Вставить(ИндексКолонки, Тип("ВыбранноеПолеКомпоновкиДанных"));
		КонецЕсли;
		
		ПолеКомпоновкиДанных.Поле = Новый ПолеКомпоновкиДанных(ИмяКолонки);
		ПолеКомпоновкиДанных.Заголовок = Формат(ТекДата, ФорматнаяСтрокаДаты);
		
		// Условное оформление по дате
		ИмяКолонкиСтатуса = "с" + Формат(ИндексКолонки, "ЧГ=");
		
		// Завизированные ПЛ 
		ЭлементУсловногоОформления = КомпоновщикНастроекОтчета.Настройки.УсловноеОформление.Элементы.Добавить();
		ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", ЦветФонаЗавизированные);
		ЭлементОтбора = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.ЛевоеЗначение = КомпоновщикНастроекОтчета.Настройки.Отбор.ДоступныеПоляОтбора.Элементы.Найти(ИмяКолонкиСтатуса).Поле;
		ЭлементОтбора.ПравоеЗначение = 3;
		ЭлементОтбора.Использование = Истина;
		ПолеОформления = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
		ПолеОформления.Использование = Истина;
		ПолеОформления.Поле = КомпоновщикНастроекОтчета.Настройки.Отбор.ДоступныеПоляОтбора.Элементы.Найти(ИмяКолонки).Поле;
		
		// Рассчитанные ПЛ 
		ЭлементУсловногоОформления = КомпоновщикНастроекОтчета.Настройки.УсловноеОформление.Элементы.Добавить();
		ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", ЦветФонаРассчитанные);
		ЭлементОтбора = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
		ЭлементОтбора.ЛевоеЗначение = КомпоновщикНастроекОтчета.Настройки.Отбор.ДоступныеПоляОтбора.Элементы.Найти(ИмяКолонкиСтатуса).Поле;
		ЭлементОтбора.ПравоеЗначение = Новый СписокЗначений;
		ЭлементОтбора.ПравоеЗначение.Добавить(1);
		ЭлементОтбора.ПравоеЗначение.Добавить(2);
		ЭлементОтбора.Использование = Истина;
		ПолеОформления = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
		ПолеОформления.Использование = Истина;
		ПолеОформления.Поле = КомпоновщикНастроекОтчета.Настройки.Отбор.ДоступныеПоляОтбора.Элементы.Найти(ИмяКолонки).Поле;
		
		// Не подписанные ПЛ 
		ЭлементУсловногоОформления = КомпоновщикНастроекОтчета.Настройки.УсловноеОформление.Элементы.Добавить();
		ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", ЦветФонаНеПодписанные);
		ЭлементОтбора = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.ЛевоеЗначение = КомпоновщикНастроекОтчета.Настройки.Отбор.ДоступныеПоляОтбора.Элементы.Найти(ИмяКолонкиСтатуса).Поле;
		ЭлементОтбора.ПравоеЗначение = -5;
		ЭлементОтбора.Использование = Истина;
		ПолеОформления = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
		ПолеОформления.Использование = Истина;
		ПолеОформления.Поле = КомпоновщикНастроекОтчета.Настройки.Отбор.ДоступныеПоляОтбора.Элементы.Найти(ИмяКолонки).Поле;
		
		//// Пересечени Н и К (К/Н, Н/К/Н...)
		//ЭлементУсловногоОформления = КомпоновщикНастроекОтчета.Настройки.УсловноеОформление.Элементы.Добавить();
		////ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", ЦветФонаПересечения);
		//ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветФонаПересечения);
		//ГруппаОтбора = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
		//ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;
		//ЭлементОтбора = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		//ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Содержит;
		//ЭлементОтбора.ЛевоеЗначение = КомпоновщикНастроекОтчета.Настройки.Отбор.ДоступныеПоляОтбора.Элементы.Найти(ИмяКолонки).Поле;
		//ЭлементОтбора.ПравоеЗначение = "Н";
		//ЭлементОтбора.Использование = Истина;
		//ЭлементОтбора = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		//ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Содержит;
		//ЭлементОтбора.ЛевоеЗначение = КомпоновщикНастроекОтчета.Настройки.Отбор.ДоступныеПоляОтбора.Элементы.Найти(ИмяКолонки).Поле;
		//ЭлементОтбора.ПравоеЗначение = "К";
		//ЭлементОтбора.Использование = Истина;
		//ПолеОформления = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
		//ПолеОформления.Использование = Истина;
		//ПолеОформления.Поле = КомпоновщикНастроекОтчета.Настройки.Отбор.ДоступныеПоляОтбора.Элементы.Найти(ИмяКолонки).Поле;
		
		// Пересечения по времени
		ЭлементУсловногоОформления = КомпоновщикНастроекОтчета.Настройки.УсловноеОформление.Элементы.Добавить();
		ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветФонаПересечения);
		ЭлементОтбора = ЭлементУсловногоОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.ЛевоеЗначение = КомпоновщикНастроекОтчета.Настройки.Отбор.ДоступныеПоляОтбора.Элементы.Найти(ИмяКолонкиСтатуса).Поле;
		ЭлементОтбора.ПравоеЗначение = -6;
		ЭлементОтбора.Использование = Истина;
		ПолеОформления = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
		ПолеОформления.Использование = Истина;
		ПолеОформления.Поле = КомпоновщикНастроекОтчета.Настройки.Отбор.ДоступныеПоляОтбора.Элементы.Найти(ИмяКолонки).Поле;
		
		
		ТекДата = ТекДата + 86400;
		ИндексКолонки = ИндексКолонки + 1;
	КонецЦикла;
	
	// Центрирование по датам
	ЭлементУсловногоОформления = КомпоновщикНастроекОтчета.Настройки.УсловноеОформление.Элементы.Добавить();
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ГоризонтальноеПоложение", ГоризонтальноеПоложение.Центр);
	ТекДата = НачПериода;
	ИндексКолонки = 0;
	Пока ТекДата <= КонПериода Цикл
		ИмяКолонки = "д" + Формат(ИндексКолонки, "ЧГ=");
		
		ПолеОформления = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
		ПолеОформления.Использование = Истина;
		ПолеОформления.Поле = КомпоновщикНастроекОтчета.Настройки.Отбор.ДоступныеПоляОтбора.Элементы.Найти(ИмяКолонки).Поле;
		
		ТекДата = ТекДата + 86400;
		ИндексКолонки = ИндексКолонки + 1;
	КонецЦикла;
	
	// Создание полей компоновщика по видам простоя
	ИндексКолонки = 0;
	Для каждого СтрокаТаблицаВидовПростоев Из ТаблицаВидовПростоев Цикл
		ИмяКолонки = "п" + Формат(ИндексКолонки, "ЧГ=");
		
		ПолеКомпоновкиДанных = ГруппаПростои.Элементы.Вставить(ИндексКолонки, Тип("ВыбранноеПолеКомпоновкиДанных"));
		ПолеКомпоновкиДанных.Поле = Новый ПолеКомпоновкиДанных(ИмяКолонки);
		ПолеКомпоновкиДанных.Заголовок = СокрЛП(СтрокаТаблицаВидовПростоев.ПричинаНевыхода.НаименованиеСокращенное);
		
		ИндексКолонки = ИндексКолонки + 1;
	КонецЦикла;

	// Получение данных для отчета
	ВнешниеНаборыДанных = Новый Структура;
	ВнешниеНаборыДанных.Вставить("ТаблицаДанных", ПолучитьТаблицуДанныхОтчета(ТаблицаПростоев, ТаблицаВидовПростоев));
	
	// Формирование отчета
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	
	
	//НастройкиКомпоновкиДанных.ПараметрыДанных.УстановитьЗначениеПараметра("НачалоПериодаОтчета", НачПериода);
	//КомпоновщикНастроекОтчета.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("НачалоПериодаОтчета", НачПериода);
	КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("НачалоПериодаОтчета", НачПериода);
	
	КомпоновщикНастроекОтчета.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("ДнейПоОтчету", (НачалоДня(КонПериода) - НачалоДня(НачПериода))/86400 + 1);
	
	НастройкиКомпоновкиДанных = КомпоновщикНастроекОтчета.ПолучитьНастройки();
	
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиКомпоновкиДанных, ДанныеРасшифровки);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, ДанныеРасшифровки);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.НачатьВывод();
	ПроцессорВывода.Вывести(ПроцессорКомпоновки, Истина);
	ПроцессорВывода.ЗакончитьВывод();

КонецПроцедуры

Функция ПолучитьТаблицуДанныхОтчета(ТаблицаПростоев, ТаблицаВидовПростоев)
	
	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить("Организация", 		Новый ОписаниеТипов("СправочникСсылка.Организации"));
	Результат.Колонки.Добавить("Колонна", 			Новый ОписаниеТипов("СправочникСсылка.уатКолонны"));
	//Результат.Колонки.Добавить("КолоннаФакт", 		Новый ОписаниеТипов("СправочникСсылка.уатКолонны"));
	Результат.Колонки.Добавить("ТранспортноеСредство", Новый ОписаниеТипов("СправочникСсылка.уатТС"));
	Результат.Колонки.Добавить("Модель", 			Новый ОписаниеТипов("СправочникСсылка.уатМоделиТС"));
	
	Результат.Колонки.Добавить("НеПодписаноПЛ", 	ОписаниеТипаЧисло);
	Результат.Колонки.Добавить("ГодВыпуска", 		ОписаниеТипаЧисло);
	Результат.Колонки.Добавить("Возраст", 			ОписаниеТипаЧисло);
	Результат.Колонки.Добавить("ДниВсего", 			ОписаниеТипаЧисло);
	Результат.Колонки.Добавить("ДниВНаряде", 		ОписаниеТипаЧисло);
	Результат.Колонки.Добавить("ДниВРемонте", 		ОписаниеТипаЧисло);
	
	// Профрешение 19.09.2024 г. { 
	Результат.Колонки.Добавить("ДниВЭксплуатации", 	ОписаниеТипаЧисло);
	Результат.Колонки.Добавить("ДниРЛ_СП_ДПНТ", 	ОписаниеТипаЧисло);
	// } Профрешение 19.09.2024 г.
	
	Результат.Колонки.Добавить("ДниВПростое", 		ОписаниеТипаЧисло);
	Результат.Колонки.Добавить("Внимание", 			Новый ОписаниеТипов("Булево"));
	Результат.Колонки.Добавить("ТСВРаботе", 		Новый ОписаниеТипов("Булево"));
	Результат.Колонки.Добавить("ТСПростойВРемонте", Новый ОписаниеТипов("Булево"));
	Результат.Колонки.Добавить("ТСПростойПрочее", 	Новый ОписаниеТипов("Булево"));
	Результат.Колонки.Добавить("Приоритет", 		ОписаниеТипаЧисло);
	Результат.Колонки.Добавить("ПричинаНевыхода", 	Новый ОписаниеТипов("СправочникСсылка.прПричиныНевыходаТС"));
	
	ТекДата = НачПериода;
	ИндексКолонки = 0;
	Пока ТекДата <= КонПериода Цикл
		ИмяКолонки = "д" + Формат(ИндексКолонки, "ЧГ=");
		Результат.Колонки.Добавить(ИмяКолонки, ОписаниеТипаСтрока);
	
		ИмяКолонки = "с" + Формат(ИндексКолонки, "ЧГ=");
		Результат.Колонки.Добавить(ИмяКолонки, ОписаниеТипаЧисло);
		
		ТекДата = ТекДата + 86400;
		ИндексКолонки = ИндексКолонки + 1;
	КонецЦикла;
	
	ИндексКолонки = 0;
	Для каждого СтрокаТаблицаВидовПростоев Из ТаблицаВидовПростоев Цикл
		ИмяКолонки = "п" + Формат(ИндексКолонки, "ЧГ=");
		Результат.Колонки.Добавить(ИмяКолонки, ОписаниеТипаЧисло);
		
		ИндексКолонки = ИндексКолонки + 1;
	КонецЦикла;
	Результат.Колонки.Добавить("пв", ОписаниеТипаЧисло);
	
	// Заполнение таблицы
	ТаблицаВладения     = ПолучитьТаблицуВладенияТранспортныхСредств(НачалоДня(НачПериода), КонецДня(КонПериода));
	ТаблицаЭксплуатации = ПолучитьТаблицуЭксплуатацииТранспортныхСредств(НачалоДня(НачПериода), КонецДня(КонПериода));
	ТаблицаЭксплуатации.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ТаблицаЭксплуатации.Колонки.Добавить("Колонна", Новый ОписаниеТипов("СправочникСсылка.уатКолонны"));
	//ТаблицаЭксплуатации.Колонки.Добавить("КолоннаФакт", Новый ОписаниеТипов("СправочникСсылка.уатКолонны"));
	
	СтруктураПоиска = Новый Структура("Дата, ТранспортноеСредство"); 
	Для каждого СтрокаТаблицаЭксплуатации Из ТаблицаЭксплуатации Цикл
		
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрокаТаблицаЭксплуатации);
		СтрокиТаблицаВладения = ТаблицаВладения.НайтиСтроки(СтруктураПоиска);
		
		Если СтрокиТаблицаВладения.Количество() > 0 Тогда                    
			
			СтрокаТаблицаЭксплуатации.Организация 	= СтрокиТаблицаВладения[0].Организация;
			СтрокаТаблицаЭксплуатации.Колонна 		= СтрокиТаблицаВладения[0].Колонна;
			//СтрокаТаблицаЭксплуатации.КолоннаФакт 	= СтрокиТаблицаВладения[0].КолоннаФакт;
			
		КонецЕсли;
		
	КонецЦикла;
	
	// Корректировка закрепления простоев по данным таблицы владения
	Для каждого СтрокаТаблицаПростоев Из ТаблицаПростоев Цикл
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрокаТаблицаПростоев);
		СтрокиТаблицаВладения = ТаблицаВладения.НайтиСтроки(СтруктураПоиска);
		Если СтрокиТаблицаВладения.Количество() > 0 Тогда
			СтрокаТаблицаПростоев.Организация = СтрокиТаблицаВладения[0].Организация;
			СтрокаТаблицаПростоев.Колонна = СтрокиТаблицаВладения[0].Колонна;
//			СтрокаТаблицаПростоев.КолоннаФакт = СтрокиТаблицаВладения[0].КолоннаФакт;
		КонецЕсли;
	КонецЦикла;
	
	// Владение
	СтруктураПоиска = Новый Структура("Организация, Колонна, ТранспортноеСредство, Модель");
	Для каждого СтрокаТаблицаВладения Из ТаблицаВладения Цикл
		
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрокаТаблицаВладения);
		СтрокиРезультат = Результат.НайтиСтроки(СтруктураПоиска);
		Если СтрокиРезультат.Количество() > 0 Тогда
			СтрокаРезультат = СтрокиРезультат[0];
		Иначе
			СтрокаРезультат = Результат.Добавить();
		    ЗаполнитьЗначенияСвойств(СтрокаРезультат, СтруктураПоиска);
		КонецЕсли;
		
		СтрокаРезультат.ДниВсего = СтрокаРезультат.ДниВсего + СтрокаТаблицаВладения.КоличествоДней;
		
		// Профрешение 19.09.2024 г. {
		СтрокаРезультат.ДниВЭксплуатации = СтрокаРезультат.ДниВЭксплуатации + СтрокаТаблицаВладения.КоличествоДней;
		// } Профрешение 19.09.2024 г.
		
	КонецЦикла;
	
	ОжиданиеРемонта = Справочники.прПричиныНевыходаТС.НайтиПоКоду("000000004");
	
	// Эксплуатация
	СтруктураПоискаТаблицаПростоев = Новый Структура("Дата, ТранспортноеСредство");
	Для каждого СтрокаТаблицаЭксплуатации Из ТаблицаЭксплуатации Цикл
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрокаТаблицаЭксплуатации);
		СтрокиРезультат = Результат.НайтиСтроки(СтруктураПоиска);
		Если СтрокиРезультат.Количество() > 0 Тогда
			СтрокаРезультат = СтрокиРезультат[0];
		Иначе
			СтрокаРезультат = Результат.Добавить();
		    ЗаполнитьЗначенияСвойств(СтрокаРезультат, СтруктураПоиска);
		КонецЕсли;
		
		ИндексКолонки = (НачалоДня(СтрокаТаблицаЭксплуатации.Дата) - НачалоДня(НачПериода))/86400;
		
		ИмяКолонки = "д" + Формат(ИндексКолонки, "ЧГ=");
		
		// Изменено 04.04.2020
		
		//Если СтрокаТаблицаЭксплуатации.ТипОбозначения = 2 Тогда
		//	СтрокаОбозначения = "Н/К";
		//Иначе
		//	СтрокаОбозначения = ?(СтрокаТаблицаЭксплуатации.ТипОбозначения = 1, "К", "Н");
		//КонецЕсли;
		//СтрокаРезультат[ИмяКолонки] = ?(СтрокаТаблицаЭксплуатации.КоличествоДней > 0, СтрокаОбозначения, "");
		СтрокаРезультат[ИмяКолонки] = СтрокаТаблицаЭксплуатации.Обозначение;
		// Изменено 04.04.2020
		
		ИмяКолонки = "с" + Формат(ИндексКолонки, "ЧГ=");
		СтрокаРезультат[ИмяКолонки] = СтрокаТаблицаЭксплуатации.Статус;
		
		СтрокаРезультат.ДниВНаряде = СтрокаРезультат.ДниВНаряде + СтрокаТаблицаЭксплуатации.КоличествоДней;
		
		// Корректировка таблицы простоев по причине ремонта
		ЗаполнитьЗначенияСвойств(СтруктураПоискаТаблицаПростоев, СтрокаТаблицаЭксплуатации);
		СтрокиТаблицаПростоев = ТаблицаПростоев.НайтиСтроки(СтруктураПоискаТаблицаПростоев);
		Для ц = -СтрокиТаблицаПростоев.Количество() + 1 По 0 Цикл
			Если СтрокиТаблицаПростоев[-ц].ПричинаНевыхода = Справочники.прПричиныНевыходаТС.Ремонт ИЛИ
				 СтрокиТаблицаПростоев[-ц].ПричинаНевыхода = ОжиданиеРемонта Тогда
				ИмяКолонки = "д" + Формат(ИндексКолонки, "ЧГ=");
				СтрокаРезультат[ИмяКолонки] = СтрокаРезультат[ИмяКолонки] + ?(ПустаяСтрока(СтрокаРезультат[ИмяКолонки]), "", "/") + СокрЛП(СтрокиТаблицаПростоев[-ц].ПричинаНевыхода.НаименованиеСокращенное);
				ТаблицаПростоев.Удалить(СтрокиТаблицаПростоев[-ц]);
			КонецЕсли;
		КонецЦикла;
		
		Если НачалоДня(СтрокаТаблицаЭксплуатации.Дата) = НачалоДня(КонПериода) Тогда
		    СтрокаРезультат.ТСВРаботе = Истина;
			СтрокаРезультат.Приоритет = 100;
		КонецЕсли;
	КонецЦикла;
	
	// Простои
	Для каждого СтрокаТаблицаПростоев Из ТаблицаПростоев Цикл
		СтрокаТаблицаВидовПростоев = ТаблицаВидовПростоев.Найти(СтрокаТаблицаПростоев.ПричинаНевыхода);
		Если СтрокаТаблицаВидовПростоев = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрокаТаблицаПростоев);
		СтрокиРезультат = Результат.НайтиСтроки(СтруктураПоиска);
		Если СтрокиРезультат.Количество() > 0 Тогда
			СтрокаРезультат = СтрокиРезультат[0];
		Иначе
			СтрокаРезультат = Результат.Добавить();
		    ЗаполнитьЗначенияСвойств(СтрокаРезультат, СтруктураПоиска);
		КонецЕсли;
		
		ИндексКолонки = (НачалоДня(СтрокаТаблицаПростоев.Дата) - НачалоДня(НачПериода))/86400;
		ИмяКолонки = "д" + Формат(ИндексКолонки, "ЧГ=");
		Для й=1 По СтрокаТаблицаПростоев.Количество Цикл
			СтрокаРезультат[ИмяКолонки] = СтрокаРезультат[ИмяКолонки] + ?(ПустаяСтрока(СтрокаРезультат[ИмяКолонки]), "", "/") + СокрЛП(СтрокаТаблицаПростоев.ПричинаНевыхода.НаименованиеСокращенное);
		КонецЦикла;
		
		ИндексКолонки = ТаблицаВидовПростоев.Индекс(СтрокаТаблицаВидовПростоев);
		ИмяКолонки = "п" + Формат(ИндексКолонки, "ЧГ=");
		СтрокаРезультат[ИмяКолонки] = СтрокаРезультат[ИмяКолонки] + СтрокаТаблицаПростоев.Количество;
		СтрокаРезультат.ДниВПростое = СтрокаРезультат.ДниВПростое + СтрокаТаблицаПростоев.Количество;
		
		// Профрешение 19.09.2024 г. {
		Если Найти(",РЛ,СП,ДПНТ,", "," + СтрокаТаблицаПростоев.ПричинаНевыхода.НаименованиеСокращенное + ",") <> 0 Тогда 
			
			СтрокаРезультат.ДниРЛ_СП_ДПНТ = СтрокаРезультат.ДниРЛ_СП_ДПНТ 
				+ СтрокаТаблицаПростоев.Количество; 
				
			// Не учитываем в эксплуатации данное ТС
			СтрокаРезультат.ДниВЭксплуатации = 0;
				
		Иначе
				
			Если СтрокаТаблицаПростоев.ПричинаНевыхода.КорректироватьКТГ Тогда
				СтрокаРезультат.ДниВРемонте = СтрокаРезультат.ДниВРемонте 
					+ СтрокаТаблицаПростоев.Количество;
			КонецЕсли;
			
		КонецЕсли;
		// } Профрешение 19.09.2024 г.
		
		Если НЕ СтрокаРезультат.ТСВРаботе И
			 НачалоДня(СтрокаТаблицаПростоев.Дата) = НачалоДня(КонПериода) Тогда
			 
			СтрокиТаблицаВладения = ТаблицаВладения.НайтиСтроки(СтруктураПоиска);
			Если СтрокиТаблицаВладения.Количество() > 0 И
				 СтрокиТаблицаВладения[0].КоличествоДней > 0 Тогда
				 
				Если СтрокаТаблицаПростоев.ПричинаНевыхода = Справочники.прПричиныНевыходаТС.Ремонт ИЛИ
					 СтрокаТаблицаПростоев.ПричинаНевыхода = ОжиданиеРемонта Тогда
					СтрокаРезультат.ТСПростойВРемонте = Истина;
				КонецЕсли;
				
				СтрокаРезультат.ТСПростойПрочее = НЕ СтрокаРезультат.ТСПростойВРемонте;
			КонецЕсли;
			
			СтрокаРезультат.ПричинаНевыхода = СтрокаТаблицаПростоев.ПричинаНевыхода;
		КонецЕсли;
		
		Если НачалоДня(СтрокаТаблицаПростоев.Дата) = НачалоДня(КонПериода) И
			 СтрокаРезультат.Приоритет = 0 Тогда
			СтрокаРезультат.Приоритет = ПолучитьПриоритетДляПростоя(СтрокаТаблицаПростоев.ПричинаНевыхода);
		КонецЕсли;
	КонецЦикла;
	
	ТаблицаПересекающихсяПутевыхЛистов = ПолучитьТаблицуПересекающихсяПутевыхЛистов(НачПериода, КонПериода);
	СтруктураПоиска.Очистить();
	
	Для каждого СтрокаРезультат Из Результат Цикл
		СтрокаРезультат.Внимание = (СтрокаРезультат.ДниВсего - СтрокаРезультат.ДниВНаряде - СтрокаРезультат.ДниВПростое <> 0);
		
		СтруктураПоиска.Вставить("ТранспортноеСредство", СтрокаРезультат.ТранспортноеСредство);
		СтруктураПоиска.Вставить("Организация", СтрокаРезультат.Организация);
		СтруктураПоиска.Вставить("Колонна", СтрокаРезультат.Колонна);
		//СтруктураПоиска.Вставить("КолоннаФакт", СтрокаРезультат.КолоннаФакт);
		СтруктураПоиска.Вставить("Модель", СтрокаРезультат.Модель);
		СтрокиТаблицаПересекающихсяПутевыхЛистов = ТаблицаПересекающихсяПутевыхЛистов.НайтиСтроки(СтруктураПоиска);
		Если СтрокиТаблицаПересекающихсяПутевыхЛистов.Количество() > 0 Тогда
			//СтрокаРезультат.Внимание = Истина;
			
			Для каждого СтрокаТаблицаПересекающихсяПутевыхЛистов Из СтрокиТаблицаПересекающихсяПутевыхЛистов Цикл
				Если ТипЗнч(СтрокаТаблицаПересекающихсяПутевыхЛистов.ПутевойЛист) = Тип("ДокументСсылка.прАктОКонсервацииТехники") Тогда
					ДатаВыезда1 = СтрокаТаблицаПересекающихсяПутевыхЛистов.ПутевойЛист.ДатаНачала;
					ДатаВозврата1 = СтрокаТаблицаПересекающихсяПутевыхЛистов.ПутевойЛист.ДатаОкончания;
				Иначе
					ДатаВыезда1 = СтрокаТаблицаПересекающихсяПутевыхЛистов.ПутевойЛист.ДатаВыезда;
					ДатаВозврата1 = СтрокаТаблицаПересекающихсяПутевыхЛистов.ПутевойЛист.ДатаВозвращения;
				КонецЕсли;
					
				Если ТипЗнч(СтрокаТаблицаПересекающихсяПутевыхЛистов.ПутевойЛист2) = Тип("ДокументСсылка.прАктОКонсервацииТехники") Тогда
					ДатаВыезда2 = СтрокаТаблицаПересекающихсяПутевыхЛистов.ПутевойЛист2.ДатаНачала;
					ДатаВозврата2 = СтрокаТаблицаПересекающихсяПутевыхЛистов.ПутевойЛист2.ДатаОкончания;
				Иначе
					ДатаВыезда2 = СтрокаТаблицаПересекающихсяПутевыхЛистов.ПутевойЛист2.ДатаВыезда;
					ДатаВозврата2 = СтрокаТаблицаПересекающихсяПутевыхЛистов.ПутевойЛист2.ДатаВозвращения;
				КонецЕсли;
				
				ДатаНачалаПересечения = Макс(ДатаВыезда1, ДатаВыезда2);
				ДатаОкончанияПересечения = Мин(ДатаВозврата1, ДатаВозврата2);
				
				Если ЗначениеЗаполнено(ДатаНачалаПересечения) И
					 ЗначениеЗаполнено(ДатаОкончанияПересечения) Тогда
					
					ТекДата = ДатаНачалаПересечения;
					Пока ТекДата <= ДатаОкончанияПересечения Цикл
						Если ТекДата < НачалоДня(НачПериода) ИЛИ
							 ТекДата > КонецДня(КонПериода) Тогда
							ТекДата = ТекДата + 86400;
							Продолжить;
						КонецЕсли;
						ИндексКолонки = (НачалоДня(ТекДата) - НачалоДня(НачПериода))/86400;
						ИмяКолонки = "д" + Формат(ИндексКолонки, "ЧГ=");
						
						//Если Найти(СтрокаРезультат[ИмяКолонки], "Н/Н") = 0 Тогда
						//	СтрокаРезультат[ИмяКолонки] = СтрЗаменить(СтрокаРезультат[ИмяКолонки], "Н", "Н/Н");
						//КонецЕсли;
						
						// Добавлено отражение пересечения
						ИмяКолонки = "с" + Формат(ИндексКолонки, "ЧГ=");
						СтрокаРезультат[ИмяКолонки] = -6;
						// Добавлено отражение пересечения
						
						ТекДата = ТекДата + 86400;
					КонецЦикла;
				КонецЕсли;
			КонецЦикла;
			
		КонецЕсли;
	КонецЦикла;
	
	// Заполнение неподписанных ПЛ
	//ТаблицаНеподписанныхПЛ = ПолучитьТаблицуНеподписанныхПЛ(ТаблицаВладения);
	//СтруктураПоиска = Новый Структура("Организация, Колонна, ТранспортноеСредство, Модель");
	//Для каждого СтрокаТаблицаНеподписанныхПЛ Из ТаблицаНеподписанныхПЛ Цикл
	//	ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрокаТаблицаНеподписанныхПЛ);
	//	СтрокиРезультат = Результат.НайтиСтроки(СтруктураПоиска);
	//	Если СтрокиРезультат.Количество() > 0 Тогда
	//		СтрокиРезультат[0].НеПодписаноПЛ = СтрокаТаблицаНеподписанныхПЛ.НеПодписаноПЛ;
	//	КонецЕсли;
	//КонецЦикла;
	
	// Заполнение дополнительных полей
	СтруктураПоиска = Новый Структура;
	Результат.Колонки.Добавить("ГосударственныйНомер");
	Для каждого СтрокаРезультат Из Результат Цикл
		//СтруктураПоиска.Вставить("ОсновноеСредство", СтрокаРезультат.ТранспортноеСредство);
		//ТранспортноеСредство = Справочники.уатТС.НайРегистрыСведений.уатПервоначальныеСведенияТС.Получить(СтруктураПоиска);
		СтрокаРезультат.ГосударственныйНомер = СтрокаРезультат.ТранспортноеСредство.ГосударственныйНомер;
		
		СтрокаРезультат.ГодВыпуска = СтрокаРезультат.ТранспортноеСредство.ГодВыпуска;
		СтрокаРезультат.Возраст = ?(СтрокаРезультат.ТранспортноеСредство.ГодВыпуска = 0 ИЛИ Год(НачПериода) < СтрокаРезультат.ТранспортноеСредство.ГодВыпуска, 0, Год(НачПериода) - СтрокаРезультат.ТранспортноеСредство.ГодВыпуска);
	КонецЦикла;
	
	Если НЕ Организация.Пустая() Тогда
		Для й = -Результат.Количество()+1 По 0 Цикл
			Если Результат[-й].Организация <> Организация Тогда
				Результат.Удалить(-й);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

Функция ПолучитьТаблицуВладенияТранспортныхСредств(НачалоПериода, КонецПериода, ВключатьПрицепы = Ложь, МассивТС = Неопределено) Экспорт 
	
	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить("Дата", Новый ОписаниеТипов("Дата"));
	Результат.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	Результат.Колонки.Добавить("Колонна", Новый ОписаниеТипов("СправочникСсылка.уатКолонны"));
	//Результат.Колонки.Добавить("КолоннаФакт", Новый ОписаниеТипов("СправочникСсылка.уатКолонны"));
	Результат.Колонки.Добавить("ТранспортноеСредство", Новый ОписаниеТипов("СправочникСсылка.уатТС"));
	Результат.Колонки.Добавить("Модель", Новый ОписаниеТипов("СправочникСсылка.уатМоделиТС"));
	Результат.Колонки.Добавить("КоличествоДней", Новый ОписаниеТипов("Число"));
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачПериода", НачалоДня(НачалоПериода));
	Запрос.УстановитьПараметр("КонПериода", КонецДня(КонецПериода));
	Запрос.УстановитьПараметр("МассивТС", МассивТС);
	Запрос.УстановитьПараметр("ПоВсемТС", МассивТС = Неопределено ИЛИ МассивТС.Количество() = 0);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	уатТС.Ссылка КАК ОсновноеСредство,
	               |	уатТС.Модель КАК Модель,
	               |	уатТС.ДатаВводаВЭксплуатацию КАК ДатаВводаВЭксплуатацию,
	               |	уатТС.ДатаВыбытия КАК ДатаВыбытия
	               |ПОМЕСТИТЬ ВТ_ДанныеТС
	               |ИЗ
	               |	Справочник.уатТС КАК уатТС
	               |ГДЕ
	               |	(уатТС.Ссылка В (&МассивТС)
	               |			ИЛИ &ПоВсемТС)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	&НачПериода КАК Период,
	               |	уатМестонахождениеТССрезПоследних.ТС КАК ТранспортноеСредство,
	               |	ВТ_ДанныеТС.Модель КАК Модель,
	               |	уатМестонахождениеТССрезПоследних.Организация КАК Организация,
	               |	уатМестонахождениеТССрезПоследних.Колонна КАК Колонна,
	               |	ВТ_ДанныеТС.ДатаВыбытия КАК ДатаВыбытия,
	               |	ВТ_ДанныеТС.ДатаВводаВЭксплуатацию КАК ДатаВводаВЭксплуатацию
	               |ИЗ
	               |	РегистрСведений.уатМестонахождениеТС.СрезПоследних(&НачПериода, ) КАК уатМестонахождениеТССрезПоследних
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ДанныеТС КАК ВТ_ДанныеТС
	               |		ПО уатМестонахождениеТССрезПоследних.ТС = ВТ_ДанныеТС.ОсновноеСредство
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	уатМестонахождениеТС.Период,
	               |	уатМестонахождениеТС.ТС,
	               |	ВТ_ДанныеТС.Модель,
	               |	уатМестонахождениеТС.Организация,
	               |	уатМестонахождениеТС.Колонна,
	               |	ВТ_ДанныеТС.ДатаВыбытия,
	               |	ВТ_ДанныеТС.ДатаВводаВЭксплуатацию
	               |ИЗ
	               |	РегистрСведений.уатМестонахождениеТС КАК уатМестонахождениеТС
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ДанныеТС КАК ВТ_ДанныеТС
	               |		ПО (ВТ_ДанныеТС.ОсновноеСредство = уатМестонахождениеТС.ТС)
	               |ГДЕ
	               |	уатМестонахождениеТС.Период МЕЖДУ &НачПериода И &КонПериода
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ТранспортноеСредство,
	               |	Период
	               |ИТОГИ ПО
	               |	ТранспортноеСредство
	               |АВТОУПОРЯДОЧИВАНИЕ";
	ВыборкаПоТранспортнымСредствам = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаПоТранспортнымСредствам.Следующий() Цикл
		ТекПериод = Неопределено;
		ТекОрганизация = Неопределено;
		ТекКолонна = Неопределено;
		
		ДатаВводаВЭксплуатацию = '00010101';
		ДатаВыбытия = '00010101';
		
		Выборка = ВыборкаПоТранспортнымСредствам.Выбрать();
		Пока Выборка.Следующий() Цикл
			ДатаВводаВЭксплуатацию = Выборка.ДатаВводаВЭксплуатацию;
			ДатаВыбытия = Выборка.ДатаВыбытия;
			Модель = Выборка.Модель;
			
			Если НЕ ВключатьПрицепы И
				(Модель.ТипТС.ВидТС = Перечисления.уатВидыТС.Прицеп ИЛИ
				Модель.ТипТС.ВидТС = Перечисления.уатВидыТС.ПрицепСамосвал ИЛИ
				Модель.ТипТС.ВидТС = Перечисления.уатВидыТС.ПрицепЦистерна ИЛИ
				Модель.ТипТС.ВидТС = Перечисления.уатВидыТС.Полуприцеп) Тогда
				Продолжить;
			КонецЕсли;
			
			Если ТекПериод = Неопределено Тогда
				ТекПериод = Выборка.Период;
				ТекОрганизация = Выборка.Организация;
				ТекКолонна = Выборка.Колонна;
				Продолжить;
			КонецЕсли;
			
			Если ТекПериод <> Неопределено И
				(ТекОрганизация <> Выборка.Организация ИЛИ
				ТекКолонна <> Выборка.Колонна) Тогда
				
				ЗаполнитьПериодВладенияТранспортногоСредства(Результат, ВыборкаПоТранспортнымСредствам.ТранспортноеСредство, Модель,  ТекОрганизация, ТекКолонна, ТекПериод, Выборка.Период - 86400, ДатаВводаВЭксплуатацию, ДатаВыбытия);
				
				Если Выборка.Организация.Пустая() Тогда
					
					ТекПериод = Неопределено;
					ТекОрганизация = Неопределено;
					ТекКолонна = Неопределено;
					//ТекКолоннаФакт = Неопределено;
					
				Иначе
					
					ТекПериод = Выборка.Период;
					ТекОрганизация = Выборка.Организация;
					ТекКолонна = Выборка.Колонна;
					
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
		
		Если ТекПериод <> Неопределено Тогда
			Если НЕ ВключатьПрицепы И
				(Модель.ТипТС.ВидТС = Перечисления.уатВидыТС.Прицеп ИЛИ
				Модель.ТипТС.ВидТС = Перечисления.уатВидыТС.ПрицепСамосвал ИЛИ
				Модель.ТипТС.ВидТС = Перечисления.уатВидыТС.ПрицепЦистерна ИЛИ
				Модель.ТипТС.ВидТС = Перечисления.уатВидыТС.Полуприцеп) Тогда
			Иначе
				ЗаполнитьПериодВладенияТранспортногоСредства(Результат, ВыборкаПоТранспортнымСредствам.ТранспортноеСредство, Модель, ТекОрганизация, ТекКолонна, ТекПериод, КонецПериода, ДатаВводаВЭксплуатацию, ДатаВыбытия);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;	
	
КонецФункции // () 

Функция ПолучитьТаблицуПересекающихсяПутевыхЛистов(НачалоПериода, КонецПериода)

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонПериода", КонецПериода);
	Запрос.УстановитьПараметр("Смена1", Справочники.уатСмены.НайтиПоКоду("000000000002"));

    Запрос.Текст = "ВЫБРАТЬ
                   |	уатСостояниеТССрезПоследних.Период КАК НачалоРаботы,
                   |	ВЫБОР
                   |		КОГДА уатСостояниеТССрезПоследних.ДатаОкончания > &КонПериода
                   |				ИЛИ уатСостояниеТССрезПоследних.ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1)
                   |			ТОГДА &КонПериода
                   |		ИНАЧЕ ДОБАВИТЬКДАТЕ(уатСостояниеТССрезПоследних.ДатаОкончания, СЕКУНДА, 1)
                   |	КОНЕЦ КАК ОкончаниеРаботы,
                   |	уатСостояниеТССрезПоследних.ТС КАК ТранспортноеСредство,
                   |	уатСостояниеТССрезПоследних.Регистратор КАК Регистратор
                   |ПОМЕСТИТЬ ВТ_ПериодыРаботыТС_Предварительно
                   |ИЗ
                   |	РегистрСведений.уатСостояниеТС.СрезПоследних(&НачПериода, ) КАК уатСостояниеТССрезПоследних
                   |ГДЕ
                   |	уатСостояниеТССрезПоследних.Состояние = ЗНАЧЕНИЕ(Справочник.уатСостояниеТС.СформированПутевойЛист)
                   |	И (уатСостояниеТССрезПоследних.Регистратор ССЫЛКА Документ.уатПутевойЛист
                   |			ИЛИ уатСостояниеТССрезПоследних.Регистратор ССЫЛКА Документ.прАктОКонсервацииТехники)
                   |
                   |ОБЪЕДИНИТЬ ВСЕ
                   |
                   |ВЫБРАТЬ
                   |	уатСостояниеТС.Период,
                   |	ВЫБОР
                   |		КОГДА уатСостояниеТС.ДатаОкончания > &КонПериода
                   |				ИЛИ уатСостояниеТС.ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1)
                   |			ТОГДА &КонПериода
                   |		ИНАЧЕ ДОБАВИТЬКДАТЕ(уатСостояниеТС.ДатаОкончания, СЕКУНДА, 1)
                   |	КОНЕЦ,
                   |	уатСостояниеТС.ТС,
                   |	уатСостояниеТС.Регистратор
                   |ИЗ
                   |	РегистрСведений.уатСостояниеТС КАК уатСостояниеТС
                   |ГДЕ
                   |	уатСостояниеТС.Состояние = ЗНАЧЕНИЕ(Справочник.уатСостояниеТС.СформированПутевойЛист)
                   |	И (уатСостояниеТС.Регистратор ССЫЛКА Документ.уатПутевойЛист
                   |			ИЛИ уатСостояниеТС.Регистратор ССЫЛКА Документ.прАктОКонсервацииТехники)
                   |	И уатСостояниеТС.Период МЕЖДУ &НачПериода И &КонПериода
                   |;
                   |
                   |////////////////////////////////////////////////////////////////////////////////
                   |ВЫБРАТЬ
                   |	уатПутевойЛист.Ссылка КАК ПутевойЛист
                   |ПОМЕСТИТЬ ВТ_ПутевыеЛистыПервойСмены
                   |ИЗ
                   |	Документ.уатПутевойЛист КАК уатПутевойЛист
                   |ГДЕ
                   |	уатПутевойЛист.Ссылка В
                   |			(ВЫБРАТЬ
                   |				ВТ_ПериодыРаботыТС_Предварительно.Регистратор
                   |			ИЗ
                   |				ВТ_ПериодыРаботыТС_Предварительно КАК ВТ_ПериодыРаботыТС_Предварительно)
                   |	И уатПутевойЛист.Смена = &Смена1
                   |;
                   |
                   |////////////////////////////////////////////////////////////////////////////////
                   |ВЫБРАТЬ
                   |	ВТ_ПериодыРаботыТС.ТранспортноеСредство КАК ТранспортноеСредство,
                   |	ВТ_ПериодыРаботыТС.Регистратор КАК ПутевойЛист,
                   |	ВТ_ПериодыРаботыТССравнение.Регистратор КАК ПутевойЛист2
                   |ПОМЕСТИТЬ ВТ_Данные
                   |ИЗ
                   |	ВТ_ПериодыРаботыТС_Предварительно КАК ВТ_ПериодыРаботыТС
                   |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПериодыРаботыТС_Предварительно КАК ВТ_ПериодыРаботыТССравнение
                   |		ПО ВТ_ПериодыРаботыТС.ТранспортноеСредство = ВТ_ПериодыРаботыТССравнение.ТранспортноеСредство
                   |			И ВТ_ПериодыРаботыТС.Регистратор <> ВТ_ПериодыРаботыТССравнение.Регистратор
                   |			И ВТ_ПериодыРаботыТС.ОкончаниеРаботы > ВТ_ПериодыРаботыТССравнение.НачалоРаботы
                   |			И ВТ_ПериодыРаботыТС.НачалоРаботы < ВТ_ПериодыРаботыТССравнение.ОкончаниеРаботы
                   |			И (ВТ_ПериодыРаботыТС.Регистратор ССЫЛКА Документ.прАктОКонсервацииТехники
                   |				ИЛИ ВТ_ПериодыРаботыТССравнение.Регистратор ССЫЛКА Документ.прАктОКонсервацииТехники
                   |				ИЛИ ВТ_ПериодыРаботыТС.Регистратор ССЫЛКА Документ.уатПутевойЛист
                   |					И ВТ_ПериодыРаботыТССравнение.Регистратор ССЫЛКА Документ.уатПутевойЛист
                   |					И ВТ_ПериодыРаботыТС.Регистратор В
                   |						(ВЫБРАТЬ
                   |							ВТ_ПутевыеЛистыПервойСмены.ПутевойЛист
                   |						ИЗ
                   |							ВТ_ПутевыеЛистыПервойСмены КАК ВТ_ПутевыеЛистыПервойСмены)
                   |					И ВТ_ПериодыРаботыТССравнение.Регистратор В
                   |						(ВЫБРАТЬ
                   |							ВТ_ПутевыеЛистыПервойСмены.ПутевойЛист
                   |						ИЗ
                   |							ВТ_ПутевыеЛистыПервойСмены КАК ВТ_ПутевыеЛистыПервойСмены))
                   |ГДЕ
                   |	(ВТ_ПериодыРаботыТС.Регистратор ССЫЛКА Документ.прАктОКонсервацииТехники
                   |			ИЛИ ВТ_ПериодыРаботыТС.Регистратор В
                   |				(ВЫБРАТЬ
                   |					ВТ_ПутевыеЛистыПервойСмены.ПутевойЛист
                   |				ИЗ
                   |					ВТ_ПутевыеЛистыПервойСмены КАК ВТ_ПутевыеЛистыПервойСмены))
                   |;
                   |
                   |////////////////////////////////////////////////////////////////////////////////
                   |ВЫБРАТЬ
                   |	уатТС.Ссылка КАК ТранспортноеСредство,
                   |	уатТС.Модель КАК Модель
                   |ПОМЕСТИТЬ ВТ_ДанныеТС
                   |ИЗ
                   |	Справочник.уатТС КАК уатТС
                   |ГДЕ
                   |	уатТС.Ссылка В
                   |			(ВЫБРАТЬ
                   |				ВТ_Данные.ТранспортноеСредство
                   |			ИЗ
                   |				ВТ_Данные КАК ВТ_Данные)
                   |;
                   |
                   |////////////////////////////////////////////////////////////////////////////////
                   |ВЫБРАТЬ
                   |	уатМестонахождениеТССрезПоследних.ТС КАК ТранспортноеСредство,
                   |	уатМестонахождениеТССрезПоследних.Организация КАК Организация,
                   |	уатМестонахождениеТССрезПоследних.Колонна КАК Колонна
                   |ПОМЕСТИТЬ ВТ_МестонахождениеТС
                   |ИЗ
                   |	РегистрСведений.уатМестонахождениеТС.СрезПоследних(
                   |			&КонПериода,
                   |			ТС В
                   |				(ВЫБРАТЬ
                   |					ВТ_Данные.ТранспортноеСредство
                   |				ИЗ
                   |					ВТ_Данные КАК ВТ_Данные)) КАК уатМестонахождениеТССрезПоследних
                   |;
                   |
                   |////////////////////////////////////////////////////////////////////////////////
                   |ВЫБРАТЬ
                   |	ВТ_Данные.ТранспортноеСредство КАК ТранспортноеСредство,
                   |	ВТ_Данные.ПутевойЛист КАК ПутевойЛист,
                   |	ВТ_Данные.ПутевойЛист2 КАК ПутевойЛист2,
                   |	ЕСТЬNULL(ВТ_ДанныеТС.Модель, ЗНАЧЕНИЕ(Справочник.уатМоделиТС.ПустаяСсылка)) КАК Модель,
                   |	ЕСТЬNULL(ВТ_МестонахождениеТС.Организация, ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)) КАК Организация,
                   |	ЕСТЬNULL(ВТ_МестонахождениеТС.Колонна, ЗНАЧЕНИЕ(Справочник.уатКолонны.ПустаяСсылка)) КАК Колонна
                   |ИЗ
                   |	ВТ_Данные КАК ВТ_Данные
                   |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ДанныеТС КАК ВТ_ДанныеТС
                   |		ПО ВТ_Данные.ТранспортноеСредство = ВТ_ДанныеТС.ТранспортноеСредство
                   |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_МестонахождениеТС КАК ВТ_МестонахождениеТС
                   |		ПО ВТ_Данные.ТранспортноеСредство = ВТ_МестонахождениеТС.ТранспортноеСредство";
	
	ТаблицаРезультатаЗапроса = Запрос.Выполнить().Выгрузить();
	Результат = ТаблицаРезультатаЗапроса.СкопироватьКолонки();
	СтруктураОтбора = Новый Структура("ТранспортноеСредство, ПутевойЛист, ПутевойЛист2");
	Для каждого СтрокаТаблицаРезультатаЗапроса Из ТаблицаРезультатаЗапроса Цикл
		СтруктураОтбора.Вставить("ТранспортноеСредство", СтрокаТаблицаРезультатаЗапроса.ТранспортноеСредство);
		СтруктураОтбора.Вставить("ПутевойЛист", СтрокаТаблицаРезультатаЗапроса.ПутевойЛист2);
		СтруктураОтбора.Вставить("ПутевойЛист2", СтрокаТаблицаРезультатаЗапроса.ПутевойЛист);
		Если Результат.НайтиСтроки(СтруктураОтбора).Количество() = 0 Тогда
			СтрокаРезультат = Результат.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаРезультат, СтрокаТаблицаРезультатаЗапроса);
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;	
КонецФункции // ()    

Процедура ЗаполнитьПериодВладенияТранспортногоСредства(ТаблицаДнейВХозяйстве, ТранспортноеСредство, Модель, Организация, Колонна, НачалоПериода, КонецПериода, ДатаВводаВЭксплуатацию, ДатаВыбытия)
	ТекДата = НачалоДня(НачалоПериода);
	Пока ТекДата <= НачалоДня(КонецПериода) Цикл
		
		Если НЕ Организация.Пустая() И
			 ТекДата >= ДатаВводаВЭксплуатацию И
			 (НЕ ЗначениеЗаполнено(ДатаВыбытия) ИЛИ ТекДата < ДатаВыбытия) Тогда
			СтрокаТаблицаДнейВХозяйстве = ТаблицаДнейВХозяйстве.Добавить();
			СтрокаТаблицаДнейВХозяйстве.Дата = ТекДата;
			СтрокаТаблицаДнейВХозяйстве.Организация = Организация;
			СтрокаТаблицаДнейВХозяйстве.Колонна = Колонна;
			СтрокаТаблицаДнейВХозяйстве.ТранспортноеСредство = ТранспортноеСредство;
			СтрокаТаблицаДнейВХозяйстве.Модель = Модель;
			СтрокаТаблицаДнейВХозяйстве.КоличествоДней = 1;
		КонецЕсли;
		
		ТекДата = ТекДата + 86400;
	КонецЦикла;
КонецПроцедуры

Функция ПолучитьТаблицуНеподписанныхПЛ(ТаблицаВладения)

	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить("Организация", Новый ОписаниеТипов("СправочникСсылка.Организации"));
	Результат.Колонки.Добавить("Колонна", Новый ОписаниеТипов("СправочникСсылка.уатКолонны"));
	Результат.Колонки.Добавить("ТранспортноеСредство", Новый ОписаниеТипов("СправочникСсылка.уатТС"));
	Результат.Колонки.Добавить("Модель", Новый ОписаниеТипов("СправочникСсылка.уатМоделиТС"));
	Результат.Колонки.Добавить("НеПодписаноПЛ", ОписаниеТипаЧисло);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачПериода", НачалоДня(НачПериода));
	Запрос.УстановитьПараметр("КонПериода", КонецДня(КонПериода));
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	уатПутевойЛист.ТранспортноеСредство КАК ТранспортноеСредство,
	               |	НАЧАЛОПЕРИОДА(уатПутевойЛист.ДатаВыезда, ДЕНЬ) КАК Дата,
	               |	СУММА(1) КАК НеПодписаноПЛ,
	               |	уатТС.Модель КАК Модель
	               |ИЗ
	               |	Документ.уатПутевойЛист КАК уатПутевойЛист
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.уатТС КАК уатТС
	               |		ПО уатПутевойЛист.ОборудованиеПрицеп1 = уатТС.Ссылка
	               |ГДЕ
	               |	уатПутевойЛист.Дата МЕЖДУ &НачПериода И &КонПериода
	               |	И НЕ уатПутевойЛист.Проведен
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	уатПутевойЛист.ТранспортноеСредство,
	               |	НАЧАЛОПЕРИОДА(уатПутевойЛист.ДатаВыезда, ДЕНЬ),
	               |	уатТС.Модель";  
	СтруктураПоиска = Новый Структура("Дата, ТранспортноеСредство");
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СтрокаРезультат = Результат.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаРезультат, Выборка);
		
		ЗаполнитьЗначенияСвойств(СтруктураПоиска, Выборка);
		СтрокиТаблицаВладения = ТаблицаВладения.НайтиСтроки(СтруктураПоиска);
		Если СтрокиТаблицаВладения.Количество() > 0 Тогда
			СтрокаРезультат.Организация = СтрокиТаблицаВладения[0].Организация;
			СтрокаРезультат.Колонна = СтрокиТаблицаВладения[0].Колонна;
			//СтрокаРезультат.КолоннаФакт = СтрокиТаблицаВладения[0].КолоннаФакт;
		КонецЕсли;
	КонецЦикла;
	
	Результат.Свернуть("Организация, Колонна, ТранспортноеСредство, Модель", "НеПодписаноПЛ");
	
	Возврат Результат;	

КонецФункции // ()

Функция ПолучитьТаблицуЭксплуатацииТранспортныхСредств(НачалоПериода, КонецПериода, ВключатьПрицепы = Ложь, БезСтатуса = Ложь, ПолучитьДоходыИЧасы = Ложь, МассивТС = Неопределено) 
	
  	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить("Дата", Новый ОписаниеТипов("Дата"));
	Результат.Колонки.Добавить("ТранспортноеСредство", Новый ОписаниеТипов("СправочникСсылка.уатТС"));
	Результат.Колонки.Добавить("Модель", Новый ОписаниеТипов("СправочникСсылка.уатМоделиТС"));
	Результат.Колонки.Добавить("КоличествоДней", Новый ОписаниеТипов("Число"));
	Результат.Колонки.Добавить("Статус", Новый ОписаниеТипов("Число")); // 3 - завизированы ПЛ, 2 - согласованы ПЛ, 1 - рассчитаны ПЛ, -1 - консервация, -5 - не подписан, иначе 0
	Результат.Колонки.Добавить("ТипОбозначения", Новый ОписаниеТипов("Число")); // 0 - ПЛ, 1 - Акт о консервации, 2 - ПЛ и Акт о консервации
	Результат.Колонки.Добавить("Регистратор");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("НачПериода", НачалоДня(НачалоПериода));
	Запрос.УстановитьПараметр("КонПериода", КонецДня(КонецПериода));
	Запрос.УстановитьПараметр("МассивТС", МассивТС);
	Запрос.УстановитьПараметр("ПоВсемТС", МассивТС = Неопределено ИЛИ МассивТС.Количество() = 0);
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	уатТС.Ссылка КАК ОсновноеСредство,
	               |	уатТС.Модель КАК Модель
	               |ПОМЕСТИТЬ ВТ_ДанныеТС
	               |ИЗ
	               |	Справочник.уатТС КАК уатТС
	               |ГДЕ
	               |	(уатТС.ОсновноеСредство В (&МассивТС)
	               |			ИЛИ &ПоВсемТС)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	уатСостояниеТССрезПоследних.Период КАК НачалоРаботы,
	               |	ВЫБОР
	               |		КОГДА уатСостояниеТССрезПоследних.ДатаОкончания > &КонПериода
	               |				ИЛИ уатСостояниеТССрезПоследних.ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1)
	               |			ТОГДА &КонПериода
	               |		ИНАЧЕ уатСостояниеТССрезПоследних.ДатаОкончания
	               |	КОНЕЦ КАК ОкончаниеРаботы,
	               |	уатСостояниеТССрезПоследних.ТС КАК ТранспортноеСредство,
	               |	уатСостояниеТССрезПоследних.Регистратор КАК Регистратор
	               |ПОМЕСТИТЬ ВТ_СостоянияТС
	               |ИЗ
	               |	РегистрСведений.уатСостояниеТС.СрезПоследних(&НачПериода, ) КАК уатСостояниеТССрезПоследних
	               |ГДЕ
	               |	уатСостояниеТССрезПоследних.Состояние = ЗНАЧЕНИЕ(Справочник.уатСостояниеТС.СформированПутевойЛист)
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	уатСостояниеТС.Период,
	               |	ВЫБОР
	               |		КОГДА уатСостояниеТС.ДатаОкончания > &КонПериода
	               |				ИЛИ уатСостояниеТС.ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1)
	               |			ТОГДА &КонПериода
	               |		ИНАЧЕ уатСостояниеТС.ДатаОкончания
	               |	КОНЕЦ,
	               |	уатСостояниеТС.ТС,
	               |	уатСостояниеТС.Регистратор
	               |ИЗ
	               |	РегистрСведений.уатСостояниеТС КАК уатСостояниеТС
	               |ГДЕ
	               |	уатСостояниеТС.Период МЕЖДУ &НачПериода И &КонПериода
	               |	И уатСостояниеТС.Состояние = ЗНАЧЕНИЕ(Справочник.уатСостояниеТС.СформированПутевойЛист)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_СостоянияТС.НачалоРаботы КАК НачалоРаботы,
	               |	ВТ_СостоянияТС.ОкончаниеРаботы КАК ОкончаниеРаботы,
	               |	ВТ_СостоянияТС.ТранспортноеСредство КАК ТранспортноеСредство,
	               |	ВТ_СостоянияТС.Регистратор КАК Регистратор,
	               |	ВТ_ДанныеТС.Модель КАК Модель,
	               |	уатТипыТС.ВидТС КАК ВидТС
	               |ИЗ
	               |	ВТ_ДанныеТС КАК ВТ_ДанныеТС
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СостоянияТС КАК ВТ_СостоянияТС
	               |		ПО ВТ_ДанныеТС.ОсновноеСредство = ВТ_СостоянияТС.ТранспортноеСредство
	               |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.уатМоделиТС КАК уатМоделиТС
	               |			ЛЕВОЕ СОЕДИНЕНИЕ Справочник.уатТипыТС КАК уатТипыТС
	               |			ПО (уатТипыТС.Ссылка = уатМоделиТС.ТипТС)
	               |		ПО ВТ_ДанныеТС.Модель = уатМоделиТС.Ссылка
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	ТранспортноеСредство,
	               |	НачалоРаботы"; 
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если НЕ ВключатьПрицепы И
			(Выборка.ВидТС = Перечисления.уатВидыТС.Прицеп ИЛИ
			Выборка.ВидТС = Перечисления.уатВидыТС.ПрицепСамосвал ИЛИ
			Выборка.ВидТС = Перечисления.уатВидыТС.ПрицепЦистерна ИЛИ
			Выборка.ВидТС = Перечисления.уатВидыТС.Полуприцеп) Тогда
			Продолжить;
		КонецЕсли;
		
		ТекДата = Выборка.НачалоРаботы;
		Пока ТекДата < Выборка.ОкончаниеРаботы Цикл
			Если ТекДата >= НачалоПериода И
				ТекДата <= КонецПериода Тогда
				СтрокаРезультат = Результат.Добавить();
				СтрокаРезультат.Дата = НачалоДня(ТекДата);
				СтрокаРезультат.ТранспортноеСредство = Выборка.ТранспортноеСредство;
				СтрокаРезультат.Модель = Выборка.Модель;
				СтрокаРезультат.КоличествоДней = 0;
				СтрокаРезультат.Регистратор = Выборка.Регистратор;
				СтрокаРезультат.Статус = 0;
				СтрокаРезультат.ТипОбозначения = 0;
			КонецЕсли;
			
			// Если следующий день меньше суток, но окончание работы позже 08:00, то включим дату в табель
			Если ТекДата + 86400 >= НачалоПериода И
				ТекДата + 86400 <= КонецПериода И
				ТекДата + 86400 > Выборка.ОкончаниеРаботы И
				НачалоДня(ТекДата) < НачалоДня(Выборка.ОкончаниеРаботы) И
				Выборка.ОкончаниеРаботы >= НачалоДня(Выборка.ОкончаниеРаботы) + 8*3600-1 Тогда
				
				СтрокаРезультат = Результат.Добавить();
				СтрокаРезультат.Дата = НачалоДня(Выборка.ОкончаниеРаботы);
				СтрокаРезультат.ТранспортноеСредство = Выборка.ТранспортноеСредство;
				СтрокаРезультат.Модель = Выборка.Модель;
				СтрокаРезультат.КоличествоДней = 0;
				СтрокаРезультат.Регистратор = Выборка.Регистратор;
				СтрокаРезультат.Статус = 0;
				СтрокаРезультат.ТипОбозначения = 0;
			КонецЕсли;
			
			ТекДата = ТекДата + 86400;
		КонецЦикла;
	КонецЦикла;
	
	Результат.Свернуть("Дата, ТранспортноеСредство, Модель, Регистратор", "КоличествоДней, Статус, ТипОбозначения");
	
	Если ПолучитьДоходыИЧасы Тогда
		Запрос.УстановитьПараметр("НачПериода", ДобавитьМесяц(НачалоПериода, -1));
		Запрос.УстановитьПараметр("КонПериода", ДобавитьМесяц(КонецПериода, 12));
		Запрос.УстановитьПараметр("ПутевыеЛисты", Результат.ВыгрузитьКолонку("Регистратор")); 
		Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		               |	уатПутевойЛист.ТранспортноеСредство КАК ТранспортноеСредство,
		               |	уатПутевойЛистЗадание.Ссылка КАК ПутевойЛист,
		               |	уатПутевойЛист.Контрагент КАК Контрагент,
		               |	уатПутевойЛистЗадание.пр_Проект КАК Проект,
		               |	уатПутевойЛистЗадание.пр_ОбъектЗатрат КАК ОбъектЗатрат,
		               |	уатПутевойЛист.ДатаВыезда КАК НачалоПериода,
		               |	уатПутевойЛист.ДатаВозвращения КАК КонецПериода,
		               |	СУММА(ВЫБОР
		               |			КОГДА уатПутевойЛистЗадание.пр_Сумма <> 0
		               |				ТОГДА уатПутевойЛистЗадание.КоличествоЧасов
		               |			ИНАЧЕ 0
		               |		КОНЕЦ) КАК Часы,
		               |	СУММА(ВЫБОР
		               |			КОГДА уатПутевойЛистЗадание.пр_Сумма = 0
		               |				ТОГДА уатПутевойЛистЗадание.КоличествоЧасов
		               |			ИНАЧЕ 0
		               |		КОНЕЦ) КАК ЧасыНепроизводственные,
		               |	СУММА(уатПутевойЛистЗадание.пр_Сумма) КАК Сумма
		               |ИЗ
		               |	Документ.уатПутевойЛист.Задание КАК уатПутевойЛистЗадание
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.уатПутевойЛист КАК уатПутевойЛист
		               |		ПО уатПутевойЛистЗадание.Ссылка = уатПутевойЛист.Ссылка
		               |ГДЕ
		               |	уатПутевойЛист.Ссылка В(&ПутевыеЛисты)
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	уатПутевойЛист.ТранспортноеСредство,
		               |	уатПутевойЛистЗадание.пр_ОбъектЗатрат,
		               |	уатПутевойЛистЗадание.Ссылка,
		               |	уатПутевойЛистЗадание.пр_Проект,
		               |	уатПутевойЛист.Контрагент,
		               |	уатПутевойЛист.ДатаВыезда,
		               |	уатПутевойЛист.ДатаВозвращения
		               |
		               |ОБЪЕДИНИТЬ ВСЕ
		               |
		               |ВЫБРАТЬ
		               |	прАктОКонсервацииТехники.ТранспортноеСредство,
		               |	прАктОКонсервацииТехники.Ссылка,
		               |	прАктОКонсервацииТехники.Контрагент,
		               |	прАктОКонсервацииТехники.Проект,
		               |	прАктОКонсервацииТехники.ОбъектЗатрат,
		               |	прАктОКонсервацииТехники.ПериодС,
		               |	прАктОКонсервацииТехники.ПериодПо,
		               |	ВЫБОР
		               |		КОГДА прАктОКонсервацииТехники.Сумма <> 0
		               |			ТОГДА прАктОКонсервацииТехники.Часы
		               |		ИНАЧЕ 0
		               |	КОНЕЦ,
		               |	ВЫБОР
		               |		КОГДА прАктОКонсервацииТехники.Сумма = 0
		               |			ТОГДА прАктОКонсервацииТехники.Часы
		               |		ИНАЧЕ 0
		               |	КОНЕЦ,
		               |	прАктОКонсервацииТехники.Сумма
		               |ИЗ
		               |	Документ.прАктОКонсервацииТехники КАК прАктОКонсервацииТехники
		               |ГДЕ
		               |	прАктОКонсервацииТехники.Ссылка В(&ПутевыеЛисты)";
		
		Результат.Колонки.Добавить("Сумма", Новый ОписаниеТипов("Число"));
		Результат.Колонки.Добавить("Часы", Новый ОписаниеТипов("Число"));
		Результат.Колонки.Добавить("ЧасыНепроизводственные", Новый ОписаниеТипов("Число"));
		
		СтруктураПоиска = Новый Структура;
		
		ТаблицаДетализации = Запрос.Выполнить().Выгрузить();
		ТаблицаТСРегистратор = ТаблицаДетализации.Скопировать();
		ТаблицаТСРегистратор.Свернуть("ТранспортноеСредство, ПутевойЛист, НачалоПериода, КонецПериода", "Часы, ЧасыНепроизводственные, Сумма");
		Для каждого СтрокаТаблицаТСРегистратор Из ТаблицаТСРегистратор Цикл
			
			СтруктураПоиска.Вставить("ТранспортноеСредство", СтрокаТаблицаТСРегистратор.ТранспортноеСредство);
			СтруктураПоиска.Вставить("Регистратор", СтрокаТаблицаТСРегистратор.ПутевойЛист);
			СтрокиРезультат = Результат.НайтиСтроки(СтруктураПоиска);
			
			// Определение доли часов и сумм для отнесения в периоде
			Если СтрокаТаблицаТСРегистратор.НачалоПериода < НачалоПериода ИЛИ
				СтрокаТаблицаТСРегистратор.КонецПериода > КонецПериода Тогда
				
				КоличествоДней = (НачалоДня(СтрокаТаблицаТСРегистратор.КонецПериода) - НачалоДня(СтрокаТаблицаТСРегистратор.НачалоПериода))/86400;
				Если КоличествоДней = 0 Тогда
					КоличествоДней = 1;
				ИначеЕсли Час(СтрокаТаблицаТСРегистратор.КонецПериода) >= 8 Тогда
					КоличествоДней = КоличествоДней + 1;
				КонецЕсли;
				
				Коэффициент = СтрокиРезультат.Количество()/КоличествоДней;
			Иначе
				Коэффициент = 1;
			КонецЕсли;
			
			БазаРаспределения = СтрокиРезультат.Количество();
			Если БазаРаспределения > 0 Тогда
				Сумма = Окр(СтрокаТаблицаТСРегистратор.Сумма * Коэффициент, 2);
				Часы = Окр(СтрокаТаблицаТСРегистратор.Часы * Коэффициент, 3);
				ЧасыНепроизводственные = Окр(СтрокаТаблицаТСРегистратор.ЧасыНепроизводственные * Коэффициент, 3);
				
				ОстатокСумма = Сумма;
				ОстатокЧасы = Часы;
				ОстатокЧасыНепроизводственные = ЧасыНепроизводственные;
				
				Для каждого СтрокаРезультат Из СтрокиРезультат Цикл
					Если СтрокаРезультат = СтрокиРезультат[СтрокиРезультат.Количество() - 1] Тогда
						РаспределитьСумма = ОстатокСумма;
						РаспределитьЧасы = ОстатокЧасы;
						РаспределитьЧасыНепроизводственные = ОстатокЧасыНепроизводственные;
					Иначе
						РаспределитьСумма = ?(ОстатокСумма >= 0, Мин(ОстатокСумма, Сумма/БазаРаспределения), Макс(ОстатокСумма, Сумма/БазаРаспределения));
						РаспределитьЧасы = ?(ОстатокЧасы >= 0, Мин(ОстатокЧасы, Часы/БазаРаспределения), Макс(ОстатокЧасы, Часы/БазаРаспределения));
						РаспределитьЧасыНепроизводственные = ?(ОстатокЧасыНепроизводственные >= 0, Мин(ОстатокЧасыНепроизводственные, ЧасыНепроизводственные/БазаРаспределения), Макс(ОстатокЧасыНепроизводственные, ЧасыНепроизводственные/БазаРаспределения));
						
						РаспределитьСумма = Окр(РаспределитьСумма, 2);
						РаспределитьЧасы = Окр(РаспределитьЧасы, 3);
						РаспределитьЧасыНепроизводственные = Окр(РаспределитьЧасыНепроизводственные, 3);
					КонецЕсли;
					
					ОстатокСумма = ОстатокСумма - РаспределитьСумма;
					ОстатокЧасы = ОстатокЧасы - РаспределитьЧасы;
					ОстатокЧасыНепроизводственные = ОстатокЧасыНепроизводственные - РаспределитьЧасыНепроизводственные;
					
					СтрокаРезультат.Сумма = РаспределитьСумма;
					СтрокаРезультат.Часы = РаспределитьЧасы;
					СтрокаРезультат.ЧасыНепроизводственные = РаспределитьЧасыНепроизводственные;
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	РезультатСводно = Результат.Скопировать();
	РезультатСводно.Свернуть("Дата, ТранспортноеСредство, Модель", "КоличествоДней, Статус, ТипОбозначения" + ?(ПолучитьДоходыИЧасы, ", Сумма, Часы, ЧасыНепроизводственные", ""));
	РезультатСводно.Колонки.Добавить("Обозначение", Новый ОписаниеТипов("Строка"));
	Если ПолучитьДоходыИЧасы Тогда
		СтруктураПоиска.Очистить();
		РезультатСводно.Колонки.Добавить("ТаблицаДетализации");
		Для каждого СтрокаРезультатСводно Из РезультатСводно Цикл
			СтрокаРезультатСводно.ТаблицаДетализации = Новый ТаблицаЗначений;
			СтрокаРезультатСводно.ТаблицаДетализации.Колонки.Добавить("Контрагент");
			СтрокаРезультатСводно.ТаблицаДетализации.Колонки.Добавить("Проект");
			СтрокаРезультатСводно.ТаблицаДетализации.Колонки.Добавить("ОбъектЗатрат");
			СтрокаРезультатСводно.ТаблицаДетализации.Колонки.Добавить("Сумма");
			СтрокаРезультатСводно.ТаблицаДетализации.Колонки.Добавить("Часы");
			СтрокаРезультатСводно.ТаблицаДетализации.Колонки.Добавить("ЧасыНепроизводственные");
			
			СтруктураПоиска.Вставить("ТранспортноеСредство", СтрокаРезультатСводно.ТранспортноеСредство);
			СтрокиТаблицаДетализацииПериод = ТаблицаДетализации.НайтиСтроки(СтруктураПоиска);
			Для каждого СтрокаТаблицаДетализацииПериод Из СтрокиТаблицаДетализацииПериод Цикл
				Если СтрокаРезультатСводно.Дата < НачалоДня(СтрокаТаблицаДетализацииПериод.НачалоПериода) Тогда
					Продолжить;
				КонецЕсли;
				Если НачалоДня(СтрокаТаблицаДетализацииПериод.НачалоПериода) < НачалоДня(СтрокаТаблицаДетализацииПериод.КонецПериода) Тогда
					КонецПериода = ?(Час(СтрокаТаблицаДетализацииПериод.КонецПериода) >= 8, НачалоДня(СтрокаТаблицаДетализацииПериод.КонецПериода), НачалоДня(НачалоДня(СтрокаТаблицаДетализацииПериод.КонецПериода)-1));
				Иначе
					КонецПериода = НачалоДня(СтрокаТаблицаДетализацииПериод.КонецПериода);
				КонецЕсли;
				Если СтрокаРезультатСводно.Дата > КонецПериода Тогда
					Продолжить;
				КонецЕсли;
				
				КоличествоДней = (НачалоДня(СтрокаТаблицаДетализацииПериод.КонецПериода) - НачалоДня(СтрокаТаблицаДетализацииПериод.НачалоПериода))/86400;
				Если КоличествоДней = 0 Тогда
					КоличествоДней = 1;
				ИначеЕсли Час(СтрокаТаблицаДетализацииПериод.КонецПериода) >= 8 Тогда
					КоличествоДней = КоличествоДней + 1;
				КонецЕсли;
				Если КоличествоДней = 0 Тогда
					Продолжить;
				КонецЕсли;
				
				СтрокаДетализации = СтрокаРезультатСводно.ТаблицаДетализации.Добавить();
				СтрокаДетализации.Контрагент = СтрокаТаблицаДетализацииПериод.Контрагент;
				СтрокаДетализации.Проект = СтрокаТаблицаДетализацииПериод.Проект;
				СтрокаДетализации.ОбъектЗатрат = СтрокаТаблицаДетализацииПериод.ОбъектЗатрат;
				СтрокаДетализации.Часы = ?(СтрокаРезультатСводно.Дата = КонецПериода, СтрокаТаблицаДетализацииПериод.Часы - Окр(СтрокаТаблицаДетализацииПериод.Часы/КоличествоДней, 3)*(КоличествоДней-1), Окр(СтрокаТаблицаДетализацииПериод.Часы/КоличествоДней, 3));
				СтрокаДетализации.ЧасыНепроизводственные = ?(СтрокаРезультатСводно.Дата = КонецПериода, СтрокаТаблицаДетализацииПериод.ЧасыНепроизводственные - Окр(СтрокаТаблицаДетализацииПериод.ЧасыНепроизводственные/КоличествоДней, 3)*(КоличествоДней-1), Окр(СтрокаТаблицаДетализацииПериод.ЧасыНепроизводственные/КоличествоДней, 3));
				СтрокаДетализации.Сумма = ?(СтрокаРезультатСводно.Дата = КонецПериода, СтрокаТаблицаДетализацииПериод.Сумма - Окр(СтрокаТаблицаДетализацииПериод.Сумма/КоличествоДней, 2)*(КоличествоДней-1), Окр(СтрокаТаблицаДетализацииПериод.Сумма/КоличествоДней, 2));
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	Если БезСтатуса Тогда
		Для каждого СтрокаРезультатСводно Из РезультатСводно Цикл
			СтрокаРезультатСводно.КоличествоДней = 1;
		КонецЦикла;
	Иначе
		СтруктураОтбора = Новый Структура("Дата, ТранспортноеСредство, Модель");
		Для каждого СтрокаРезультатСводно Из РезультатСводно Цикл
			СтрокаРезультатСводно.КоличествоДней = 1;
			
			ОбозначениеН = "";
			ОбозначениеК = "";
			
			ЗаполнитьЗначенияСвойств(СтруктураОтбора, СтрокаРезультатСводно);
			СтрокиРезультат = Результат.НайтиСтроки(СтруктураОтбора);
			Если СтрокиРезультат.Количество() > 0 Тогда
				Статус = 3;
				ЕстьПЛ = Ложь;
				ЕстьАкт = Ложь;
				Для каждого СтрокаРезультат Из СтрокиРезультат Цикл
					СтатусРегистратора = 0;
					
					Если ТипЗнч(СтрокаРезультат.Регистратор) = Тип("ДокументСсылка.уатПутевойЛист") Тогда
						ОбозначениеН = ОбозначениеН + ?(ПустаяСтрока(ОбозначениеН), "", "/") + "Н";
					КонецЕсли;
					Если ТипЗнч(СтрокаРезультат.Регистратор) = Тип("ДокументСсылка.прАктОКонсервацииТехники") Тогда
						ОбозначениеК = ОбозначениеК + ?(ПустаяСтрока(ОбозначениеК), "", "/") + "К";
					КонецЕсли;
					
					Если ТипЗнч(СтрокаРезультат.Регистратор) = Тип("ДокументСсылка.уатПутевойЛист") ИЛИ
						ТипЗнч(СтрокаРезультат.Регистратор) = Тип("ДокументСсылка.прАктОКонсервацииТехники") Тогда
						
						Если ТипЗнч(СтрокаРезультат.Регистратор) = Тип("ДокументСсылка.прАктОКонсервацииТехники") Тогда
							ЕстьАкт = Истина;
						Иначе
							ЕстьПЛ = Истина;
						КонецЕсли;
						
						Если ТипЗнч(СтрокаРезультат.Регистратор) = Тип("ДокументСсылка.уатПутевойЛист") И
							СтрокаРезультат.Регистратор.Рассчитан Тогда
							СтатусРегистратора = 1;
						КонецЕсли;
						
					КонецЕсли;
					
					Статус = Мин(Статус, СтатусРегистратора);
				КонецЦикла;
				СтрокаРезультатСводно.Статус = Статус;
				
				Если ЕстьПЛ И ЕстьАкт Тогда
					СтрокаРезультатСводно.ТипОбозначения = 2;
				Иначе
					СтрокаРезультатСводно.ТипОбозначения = ?(ЕстьПЛ, 0, 1);
				КонецЕсли;
				
				СтрокаРезультатСводно.Обозначение = ОбозначениеН + ?(ПустаяСтрока(ОбозначениеН) ИЛИ ПустаяСтрока(ОбозначениеК), "", "/") + ОбозначениеК;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат РезультатСводно;

КонецФункции // ()

Функция ПолучитьПриоритетДляПростоя(ПричинаНевыхода)

	Если НЕ ЗначениеЗаполнено(ПричинаНевыхода) Тогда
		Возврат 0;
	КонецЕсли;
	
	Результат = 0;
	Если ПричинаНевыхода = Справочники.прПричиныНевыходаТС.Реализация Тогда // реализация
		Результат = 10;
	ИначеЕсли ПричинаНевыхода = Справочники.прПричиныНевыходаТС.Списание Тогда // списание
		Результат = 9;
	ИначеЕсли ПричинаНевыхода = Справочники.прПричиныНевыходаТС.ПростойСезоннойТехники Тогда // простой сезонной техники
		Результат = 8;
	ИначеЕсли ПричинаНевыхода = Справочники.прПричиныНевыходаТС.ОжиданиеРемонта Тогда // ожидание ремонта
		Результат = 7;
	ИначеЕсли ПричинаНевыхода = Справочники.прПричиныНевыходаТС.Ремонт Тогда // ремонт
		Результат = 6;
	ИначеЕсли ПричинаНевыхода = Справочники.прПричиныНевыходаТС.ОтсутствиеВодителя Тогда // отсутствие водителя
		Результат = 5;
	ИначеЕсли ПричинаНевыхода = Справочники.прПричиныНевыходаТС.ОтсутствиеРаботы Тогда // отсутствие работы
		Результат = 4;
	КонецЕсли;
	
	Возврат Результат;
	

КонецФункции // ()





